//
// ajaxTest.viv
//
// Copyright (C) 2003 - 2022 jones@scss.tcd.ie
//
// This program is free software; you can redistribute it and/or modify it under
// the terms of the GNU General Public License as published by the Free Software Foundation;
// either version 2 of the License, or (at your option) any later version.
//
// This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
// without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
// See the GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program; if not, write to the Free Software Foundation Inc.,
// 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
//

//
// 27/09/07 first version
// 05/11/16 VivioJS
//

#include "standard.vin"

const num W = 1024;
const num H = 620;

const num GW = W/2;
const num GH = H/2;

//
// set viewport
//
setVirtualWindow(0, 0, W, H, 1);

//
// set background brush
//
setBgBrush(gray192Brush);

//
// font
//
Font smallFont = Font("sans-serif", 14, ITALIC);

//
// Graph class
//
class Graph(num x, num y, num _w, num _h) {

	num w = _w;
	num h = _h;

	Rectangle bar1 = Rectangle2(0, 0, 0, redBrush, x, y+h, w/3, -h);
	Rectangle bar2 = Rectangle2(0, 0, 0, greenBrush, x+w/3, y+h, w/3, -h);
	Rectangle bar3 = Rectangle2(0, 0, 0, blueBrush, x+w/3*2, y+h, w/3, -h);
	Rectangle bar1Txt = Rectangle2(0, 0, 0, 0, x, y+h, w/3, -h/5);
	Rectangle bar2Txt = Rectangle2(0, 0, 0, 0, x+w/3, y+h, w/3, -h/5);
	Rectangle bar3Txt = Rectangle2(0, 0, 0, 0, x+2*w/3, y+h, w/3, -h/5);
	Line2(0, 0, bluePen, x, y, 0, h+2);
	Line2(0, 0, bluePen, x-2, y+h, w+2, 0);
	Rectangle vTxt = Rectangle2(0, HLEFT, 0, 0, x, y, w, h/10, gray160Pen, smallFont);
	Rectangle tTxt = Rectangle2(0, 0, 0, 0, x, y+h, w, h/5, redPen);

	string arg[5];

	//
	// update
	// handle string of the form: "5.2.0-8+etch7 12:34:56 1 2 3"
	//
	function update(string s) {
		arg = s.split(" ");
		vTxt.setTxt(" php version: " + arg[0]);
		tTxt.setTxt(arg[1]);
		num v0 = arg[2].toNum();
		num v1 = arg[3].toNum();
		num v2 = arg[4].toNum();
		bar1.setSz(w/3, v0*h/10); bar1Txt.setTxt("%d", v0);
		bar2.setSz(w/3, v1*h/10); bar2Txt.setTxt("%d", v1);
		bar3.setSz(w/3, v2*h/10); bar3Txt.setTxt("%d", v2);
	}

	// initialise
	update("5.2.0-8+etch7 12:34:56 1 2 3");

}

//
// graph
//
Graph graph = Graph((W-GW)/2, (H-GH)/2, GW, GH);

//
// test
//
for (num i = 0; i < 10; i++) {
	wait(1);
	graph.update(sprintf("5.2.0-8+etch7 12:34:56 %d %d %d", (random() * 10) | 0, (random() * 10) | 0, (random() * 10) | 0));
}

//
// eventFire
// fired by calling vplayer.fire(s) from JavaScript
//
when ~> eventFire(string s) {
	graph.update(s);
	return 0;
}

// eof